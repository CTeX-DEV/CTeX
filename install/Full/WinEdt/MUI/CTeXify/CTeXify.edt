// CTeXify.edt v2.0 2009/09/25
// Author: Lingyun Wu (aloft@ctex.org)
// MUI Interface for ctex.exe

Requires(20061028);
PushTagsandRegisters;

  // Settings for this interface
  // Used by MUILoad(); and MUISave();
  // It does not have to exists if defaults are OK...
  LetReg(4, '%b\MUI\CTeXify\CTeXify-mui.edt');
  GetFilePath('%X',1);
  LetReg(1, '%!1\CTeXify-mui.edt');
  IfFileExists('%!1',!'Relax;',>
    "LetReg(1, '%B\MUI\CTeXify\CTeXify-mui.edt');");
  IfFileExists('%!4',!'Relax;',>
    !|CreateFolders("%b\MUI\CTeXify",1);>
      CopyFile("%!1","%!4",1,1);|);

  MUIReset('CTeXify...');

  // Main Form Size and Position
  MUISet('','width','460');
  MUISet('','height','468');
  MUISet('','position','1'); // Main Form Center

  MUIGet('','client width',9); // Get the form size
  MUIGet('','client height',8);

  // Options are in the Page Control ----------------------------------


  MUIAdd(8,8,%!9-16,%!8-8-40,'PageControl','TPageControl');

  // OK/Cancel (aligned -> independent of the dialog size) ------------

  // Modal OK and Cancel Buttons (aligned bottom right)
  MUIAdd(0,0,0,40, 'Bottom_Panel','TPanel');
    MUISet('Bottom_Panel','aligned', 'alBottom');
    MUISet('Bottom_Panel','inner bevel', '0');
    MUISet('Bottom_Panel','outer bevel', '0');
    MUIAdd(0,0,164,0, 'Modal_Panel','TPanel','Bottom_Panel');
      MUISet('Modal_Panel','aligned', 'alRight');
      MUIAdd(0,8,0,0, 'OK','TButton','Modal_Panel','&OK');
        MUISet('OK','default', '1');
        MUISet('OK','on click', !'MUIHide(1);');
      MUIGet('OK','width',7);
      MUIAdd(%!7+8,8,0,0, 'Cancel','TButton','Modal_Panel','Cancel');
        MUISet('Cancel','cancel', '1'); // ESCAPE
        MUISet('Cancel','on click', !'MUIHide(2);');
      LetRegNum(6,16+2*%!7);
      MUISet('Modal_Panel','width','%!6');
      MUIGet('OK','height',7);
      LetRegNum(6,16+%!7);
      MUISet('Bottom_Panel','height','%!6');

  // Fancy URL and e-mail Label with event handler(s) -----------------
  // Comment out if not wanted...

  MUIAdd(10,4,0,0,'email','TLabel','Bottom_Panel','aloft@ctex.org');
    MUISet('email','font color','clBlue');
    MUISet('email','font underline','1');
    MUISet('email','cursor','crHandPoint');
    MUISet('email','on click',!"Call('email');");
    MUISet('email','hint',"Send an email to the author...");
  MUIAdd(10,20,0,0,'http','TLabel','Bottom_Panel','http:/%_/www.ctex.org');
    MUISet('http','font color','clBlue');
    MUISet('http','font underline','1');
    MUISet('http','cursor','crHandPoint');
    MUISet('http','on click',!"Call('http');");
    MUISet('http','hint',"Check for the latest downloads...");

  // Pages and Options ------------------------------------------------

  MUIAdd(0,0,0,0,'Page1','TTabSheet','PageControl','Options');
  MUIAdd(0,0,0,0,'Page2','TTabSheet','PageControl','Advanced');
  MUIGet('Page1','client width',7);
  MUIGet('','text width',8,'Output driver: ');
  LetRegNum(8,%!8+12);
  LetRegNum(6,%!7-%!8-24);

  MUIAdd(8,8,%!7-16, 24+32*2, 'GroupBox_Commands','TGroupBox','Page1','Commands');
    LetRegNum(9,22);
    MUIAdd(8,%!9+4,0,0, 'Input_Label','TLabel','GroupBox_Commands','&Input format: ');
      MUISet('Input_Label','on click',!"MUISet('','active','Input_Format');");
    MUIAdd(%!8,%!9,%!6,0,'Input_Format','TComboBox','GroupBox_Commands','|LaTeX|PDFLaTeX|AMSLaTeX|TeX|PDFTeX|AMSTeX');
      MUISet('Input_Format','style','1');
      MUISet('Input_Format','index','1');
      MUISet('Input_Label','focus','Input_Format');
    LetRegNum(9,%!9+32);
    MUIAdd(8,%!9+4,0,0,'Output_Label','TLabel','GroupBox_Commands','&Output driver: ');
      MUISet('Output_Label','on click',!"MUISet('','active','Output_Driver');");
    MUIAdd(%!8,%!9,%!6,0,'Output_Driver','TComboBox','GroupBox_Commands','|dvipdfmx|dvips');
      MUISet('Output_Driver','style','1');
      MUISet('Output_Driver','index','1');
      MUISet('Output_Label','focus','Output_Driver');

  MUIGet('Page1','client width',7);
  MUIGet('GroupBox_Commands','top',9);
  MUIGet('GroupBox_Commands','height',8);
  LetRegNum(9,'%!9+%!8+8');
  LetRegNum(8,20);   // Top Margin
  MUIAdd(8,%!9,%!7-16, 24+24*9, 'GroupBox_Options','TGroupBox','Page1','Options');
    LetRegNum(9,24);   // Check Box Height+Vertical Spacing
    LetRegNum(6, '(%!7-16)'); // Check Box Width
    MUIAdd(8,%!8,%!6,0, 'CB_Quiet','TCheckBox','GroupBox_Options','Run quietly');
    LetRegNum(8, %!8+%!9);
    MUIAdd(8,%!8,%!6,0, 'CB_Bibtex','TCheckBox','GroupBox_Options','Run BibTeX to generate .bbl file');
    LetRegNum(8, %!8+%!9);
    MUIAdd(8,%!8,%!6,0, 'CB_Xref','TCheckBox','GroupBox_Options','Generate .xref file');
    LetRegNum(8, %!8+%!9);
    MUIAdd(8,%!8,%!6,0, 'CB_Nospace','TCheckBox','GroupBox_Options',"Don't run cctspace");
    LetRegNum(8, %!8+%!9);
    MUIAdd(8,%!8,%!6,0, 'CB_Tilde','TCheckBox','GroupBox_Options','Force cctspace to insert tilde (~) instead of space');
    LetRegNum(8, %!8+%!9);
    MUIAdd(8,%!8,%!6,0, 'CB_Preproc','TCheckBox','GroupBox_Options','Force (re)preprocessing of all input files');
    LetRegNum(8, %!8+%!9);
    MUIAdd(8,%!8,%!6,0, 'CB_Clean','TCheckBox','GroupBox_Options','Clean run (remove temporary files first)');
    LetRegNum(8, %!8+%!9);
    MUIAdd(8,%!8,%!6,0, 'CB_CleanOnly','TCheckBox','GroupBox_Options','Clean only (remove temporary files, ignoring other arguments)');
    LetRegNum(8, %!8+%!9);
    MUIAdd(8,%!8,%!6,0, 'CB_Prompt','TCheckBox','GroupBox_Options','Show command and options information');

  // Advanced Options -------------------------------------------------

  MUIGet('Page1','client width',7);
  MUIGet('','text width',8,'Maximum nested &depth of input file: ');
  LetRegNum(8,%!8+12);
  LetRegNum(6,%!7-%!8-24);

  MUIAdd(8,8,%!7-16, 24+32*4, 'GroupBox_Advanced','TGroupBox','Page2','Advanced Settings');
    LetRegNum(9,22);
    MUIAdd(8,%!9+4,0,0, 'MaxRuns_Label','TLabel','GroupBox_Advanced','Maximum times of &LaTeX runs: ');
      MUISet('MaxRuns_Label','on click',!"MUISet('','active','Max_Runs');");
    MUIAdd(%!8,%!9,%!6,0,'Max_Runs','TEdit','GroupBox_Advanced');
      MUISet('Max_Runs','text','4');
    LetRegNum(9,%!9+32);
    MUIAdd(8,%!9+4,0,0,'MaxDepth_Label','TLabel','GroupBox_Advanced','Maximum nested &depth of input file: ');
      MUISet('MaxDepth_Label','on click',!"MUISet('','active','Max_Depth');");
    MUIAdd(%!8,%!9,%!6,0,'Max_Depth','TEdit','GroupBox_Advanced');
      MUISet('Max_Depth','text','8');
    LetRegNum(9,%!9+32);
    MUIAdd(8,%!9+4,0,0,'Resolution_Label','TLabel','GroupBox_Advanced','Resolution (DPI): ');
      MUISet('Resolution_Label','on click',!"MUISet('','active','Resolution');");
    MUIAdd(%!8,%!9,%!6,0,'Resolution','TEdit','GroupBox_Advanced');
      MUISet('Resolution','text','600');
    LetRegNum(9,%!9+32);
    MUIAdd(8,%!9+4,0,0,'User_Label','TLabel','GroupBox_Advanced','User-defined options: ');
      MUISet('User_Label','on click',!"MUISet('','active','User_Options');");
    MUIAdd(%!8,%!9,%!6,0,'User_Options','TEdit','GroupBox_Advanced');

  JMP('end_events');

// Event Handlers for email and http label
:email:: ==============================================================
  // Send an email about this MUI
  MUIHide(2); // MS Outlook focuses the main window instead of MUI...
  SendMail('MUI - CTeXify...','',"Lingyun Wu|aloft@ctex.org","",1000);
  Return;

:http:: ===============================================================
  // Start the internet browser
  ShellExecute("Open", "http:/%_/www.ctex.org", "", "", 1);
  Return;

:Compile:: ============================================================

  StartWorking("CTeXify %N%T");

  Assign(`-options`,``);

  MUIGet("Input_Format", 'text', '1');
  IfIStr("%!1", "LaTeX", "=", |>
    Assign(`-options`,"%$(`-options`); -latex");|);
  IfIStr("%!1", "PDFLaTeX", "=", |>
    Assign(`-options`,"%$(`-options`); -pdflatex");|);
  IfIStr("%!1", "AMSLaTeX", "=", |>
    Assign(`-options`,"%$(`-options`); -amslatex");|);
  IfIStr("%!1", "TeX", "=", |>
    Assign(`-options`,"%$(`-options`); -tex");|);
  IfIStr("%!1", "PDFTeX", "=", |>
    Assign(`-options`,"%$(`-options`); -pdftex");|);
  IfIStr("%!1", "AMSTeX", "=", |>
    Assign(`-options`,"%$(`-options`); -amstex");|);

  MUIGet("Output_Driver", 'text', '2');
  IfIStr("%!2", "dvipdfmx", "=", |>
    Assign(`-options`,"%$(`-options`); -dvipdfmx");|);
  IfIStr("%!2", "dvips", "=", |>
    Assign(`-options`,"%$(`-options`); -dvips");|);

  MUIGet("CB_Quiet", 'checked', '9');
  IfNum(%!9, "1", "=", |>
    Assign(`-options`,"%$(`-options`); -quiet");|);
  MUIGet("CB_Bibtex", 'checked', '9');
  IfNum(%!9, "1", "=", |>
    Assign(`-options`,"%$(`-options`); -bibtex");|);
  MUIGet("CB_Xref", 'checked', '9');
  IfNum(%!9, "1", "=", |>
    Assign(`-options`,"%$(`-options`); -xref");|);
  MUIGet("CB_Nospace", 'checked', '9');
  IfNum(%!9, "1", "=", |>
    Assign(`-options`,"%$(`-options`); -no-cctspace");|);
  MUIGet("CB_Tilde", 'checked', '9');
  IfNum(%!9, "1", "=", |>
    Assign(`-options`,"%$(`-options`); -force-tilde");|);
  MUIGet("CB_Preproc", 'checked', '9');
  IfNum(%!9, "1", "=", |>
    Assign(`-options`,"%$(`-options`); -force-pre");|);

  MUIGet("Max_Runs", 'text', '9');
  Assign(`-options`,"%$(`-options`); -latex-runs=%!9");
  MUIGet("Max_Depth", 'text', '9');
  Assign(`-options`,"%$(`-options`); -max-depth=%!9");
  MUIGet("Resolution", 'text', '9');
  Assign(`-options`,"%$(`-options`); -dpi=%!9");
  MUIGet("User_Options", 'text', '9');
  Assign(`-options`,"%$(`-options`); %!9");

  MUIGet("CB_CleanOnly", 'checked', '9');
  IfNum(%!9, "1", "=", |>
    Assign(`-options`,"-clean");|);

  // Close the current Document in Acrobat (Reader) using DDE
  // Avoid the error message because Acrobat locks the ".pdf" file for writing...
  LetRegNum(9,0);
  IfFileLocked("%P\%N.pdf", 00110, // Acrobat is probably Locking it!<>
    |IfIStr("%!1", "PDFLaTeX", "=", "LetRegNum(9,1);");>
       IfIStr("%!1", "PDFTeX", "=", "LetRegNum(9,1);");>
       IfIStr("%!2", "dvipdfmx", "=", "LetRegNum(9,1);");>
    |);
  IfNum(%!9,1,'=',>
     !*SendMessage("%$(`Acro-Title`); - ", $0000, $0000); // Dummy Message<>
       IfOK(!|>
              DDEOpen("%$(""AcroRead"")","acroview","control");>
              DDEExe("[DocOpen(""%P\%N.pdf"")]");  // Has to be Opened via DDE DocOpen<>
              DDEExe("[DocClose(""%P\%N.pdf"")]"); // for DocClose to work...<>
  //          DDEExe("[CloseAllDocs()]"); // A bit too radical <>
              DDEClose;>
  //          Sync problem - wait until the file is released:<>
  //          Acrobat Reader may take some time to release the pdf file!<>
              StartWorking('Closing PDF Document...');>
              LetRegNum(0,0);>
              Loop(!||LetRegNum(0,%!0+1);IfNum(%!0,9999,'>','Stop;'); // Prevent an Infinite Loop<>
                IfFileLocked("%P\%N.pdf", 00110,>
                 !'SendMessage("%$(`Acro-Title`); - ", $0000, $0000);',>
                 !'Stop;',!'Stop;');||);>
              StopWorking;>
  //          Prompt('Required Iterations: %!0.'); // Testing for Problems<>
           |);*);

  IfFileExists("%b\_Err.log",!"DeleteFile('%b\_Err.log')");
  IfFileExists("%b\_Out.log",!"DeleteFile('%b\_Out.log')");
  IfFileExists("%b\_Run.log",!"DeleteFile('%b\_Run.log')");
  IfFileExists("%P\%N.dvi",!"DeleteFile('%P\%N.dvi')");
  IfFileExists("%P\%N.log",!"DeleteFile('%P\%N.log')");
  MUIGet("CB_Clean", 'checked', '9');
  IfNum(%!9, "1", "=", *>
    Run('ctex.exe -clean', '%P', 0,1, 'CTeXify ...', 0,1);*);
  MUIGet("CB_Prompt", 'checked', '9');
  IfNum(%!9, "1", "=", *>
    Prompt('Command and options:%\%\ctex.exe %0 %$(`-options`); "%N%T"', 0, 1);*);
  WinExe('', 'ctex.exe %0 %$(`-options`); "%N%T"',>
           '%P', 'CTeXify ...', 001110, 0, "", "%b\_Out.log", "%b\_Err.log");

  ErrorLogUpdate;
  OpenOutput('%b\_Run.log');
  IfFileExists('%b\_Err.log','ReadFile("%b\_Err.log", 9);','LetReg(9,"");');
  IfStr("%!9","","=",>
    !"Relax;>
      WrL('No errors were reported during the execution of ctex.exe.');>
      WrL('*** Error File is empty!');>
      IfFileExists('%b\_Out.log','ReadFile(""%b\_Out.log"", 9);','LetReg(9,"""");');>
      IfStr('%!9','','=',>
        !|WrL;>
          WrL('*** External application did not produce any console output!');>
          Relax;|,>
        !|WrL;>
          WrL('*** WinEdt captured the following console output:');>
          WrL;>
          WrL('%!9');>
          Relax;|);",>
    !"Relax;>
      WrL('Errors were reported during the execution of ctex.exe.');>
      WrL('*** WinEdt captured the following error file:');>
      WrL;>
      WrL('%!9');>
      IfFileExists('%b\_Out.log','ReadFile(""%b\_Out.log"", 9);','LetReg(9,"""");');>
      IfStr('%!9','','=',>
        !|WrL;>
          WrL('*** External application did not produce any console output!');>
          Relax;|,>
        !|WrL;>
          WrL('*** WinEdt captured the following console output:');>
          WrL;>
          WrL('%!9');>
          Relax;|);>
      Relax;");
  LetReg(9,'');
  CloseOutput;

  StopWorking;

  Return;

:end_events:: =========================================================

  // Load Previous Values ....
  MUILoad('%!4'); // It does not have to exist...

  MUIShow; // Show Modal Dialog

  IfOK('Relax;','PopTagsandRegisters;Exit;');

  // Save the Current Values ....
  MUISave('%!4');

  Call('Compile');

PopTagsandRegisters;
